Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def KAKAO_SDK_APPKEY = properties.getProperty('kakao.sdk.appkey')
def isLibraryPlugin = pluginManager.hasPlugin("com.android.library")
def isApplicationPlugin = pluginManager.hasPlugin("com.android.application")

if (isLibraryPlugin || isApplicationPlugin) {
    android {
        compileSdkVersion compileSdk_Version
        buildToolsVersion buildTools_Version

        defaultConfig {
            /* 카카오 Appkey 변수로 등록(local.properties) */
            buildConfigField "String", "KAKAO_SDK_APPKEY", properties["kakao.sdk.appkey"]
            /* Manifest에서 변수로 참조할 수 있도록 지정 */
            manifestPlaceholders = [kakaoSdkAppkey: KAKAO_SDK_APPKEY]

            if(isApplicationPlugin) {
                applicationId "com.example.smiley"
            }

            minSdkVersion minSdk_Version
            targetSdkVersion targetSdk_Version

            versionCode versionCode
            versionName versionName

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        }

        buildTypes {
            debug {
                if(isApplicationPlugin){
                    shrinkResources false
                }
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }

            release {
                if(isApplicationPlugin){
                    shrinkResources true
                }
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = '1.8'
        }
    }
}
